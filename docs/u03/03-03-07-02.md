---
title: 📌 Funciones con Retorno
---

# 🖥️ Funciones con Retorno  

En programación, el propósito de las **funciones** es **evitar la duplicación de código**, especialmente cuando una misma operación se repite con frecuencia.  

📌 **Ejemplo:** Cuando queremos **mostrar en pantalla varias veces** un mismo mensaje, podemos crear una función para ello.  

Pero las funciones van **mucho más allá**. También permiten **realizar operaciones y devolver un resultado**, para utilizarlo en distintas partes del programa.  

---
<br>

📌 **Ejemplo:** Uso de Funciones con Retorno  

Por ejemplo, la función **`abs(x)`** devuelve el **valor absoluto** de un número. Esto permite llamarla muchas veces sin necesidad de reescribir la operación.  

---

📌 **Ejemplo básico de uso de función con retorno:** 

```
Proceso usoFuncionBasica
	Definir num1, num2, suma como Entero;
	Escribir "Escribe dos números:";
	Leer num1;
	Leer num2;
	suma <- funcionSumar(num1, num2);
	Escribir "El resultado de la suma es ", suma;
FinProceso

Funcion res <- funcionSumar(valor1, valor2)
	Definir res como Entero;
	res <- valor1 + valor2;
FinFuncion
```

📌 Explicación del código:

✔ Se ha definido la función **funcionSumar** que recibe dos valores y devuelve el resultado de la suma de ambos

✔ El proceso principal realizar una **llamada** a la función Sumar, con los dos argumentos y su resultado lo **almacena** en la variable *suma*.

---
<br>

📌 **2º Ejemplo:**  

```
Proceso sumaValoresAbsolutos
    Definir num1, num2, suma, sumaVA como Entero;

    Escribir "Escribe dos números:";
    Leer num1;
    Leer num2;

    suma <- num1 + num2;
    sumaVA <- valorAbsoluto(num1) + valorAbsoluto(num2);

    Escribir "La suma es: ", suma;
    Escribir "La suma de sus valores absolutos es: ", sumaVA;
    Escribir "Adios!!!";    
FinProceso

Funcion res <- valorAbsoluto(x)
    Definir res como Entero;
    Si x > 0 Entonces
        res <- x;
    SiNo
        res <- -x;
    FinSi
FinFuncion
```

📌 Explicación del código:

✔ La función valorAbsoluto(x) recibe un número como parámetro y devuelve su valor absoluto.

✔ El proceso sumaValoresAbsolutos solicita dos números y usa la función para calcular su suma absoluta

----
<br>

📌 **Otro Ejemplo:** Función CalcularMax

El siguiente ejemplo usa una función para determinar el número más grande ingresado por el usuario en una secuencia de datos.

```
Proceso Mayor
    Definir num1, num2, max Como Entero;
    Definir salir Como Caracter;
    
    Escribir "Escribe el número 1º";
    Leer num1;
    Escribir "Ahora el número 2º";
    Leer num2;
    
    max <- CalcularMax(num1, num2);
    
    Escribir "¿Deseas salir? (S/N)";
    Leer salir;

    Mientras salir <> "S" Hacer
        num1 <- max;
        Escribir "Ahora el siguiente número";
        Leer num2;
        max <- CalcularMax(num1, num2);
        Escribir "¿Deseas salir? (S/N)";
        Leer salir;
    FinMientras
    
    Escribir "El mayor de todos es ", max;
FinProceso

Funcion res <- CalcularMax(a, b)
    Definir res como Entero;
    Si a > b Entonces
        res <- a;
    SiNo
        res <- b;
    FinSi
FinFuncion
```

---
<br>

📌 **Otro Ejemplo:** Función CalcularMax
El siguiente ejemplo usa una función para determinar el número más grande ingresado por el usuario en una secuencia de datos.

```
Proceso Mayor
    Definir num1, num2, max Como Entero;
    Definir salir Como Caracter;
    
    Escribir "Escribe el número 1º";
    Leer num1;
    Escribir "Ahora el número 2º";
    Leer num2;
    
    max <- CalcularMax(num1, num2);
    
    Escribir "¿Deseas salir? (S/N)";
    Leer salir;

    Mientras salir <> "S" Hacer
        num1 <- max;
        Escribir "Ahora el siguiente número";
        Leer num2;
        max <- CalcularMax(num1, num2);
        Escribir "¿Deseas salir? (S/N)";
        Leer salir;
    FinMientras
    
    Escribir "El mayor de todos es ", max;
FinProceso

Funcion res <- CalcularMax(a, b)
    Definir res como Entero;
    Si a > b Entonces
        res <- a;
    SiNo
        res <- b;
    FinSi
FinFuncion
```

📌 Explicación del código:

✔ La función CalcularMax(a, b) devuelve el mayor entre dos números.

✔ El proceso Mayor permite ingresar varios números y encuentra el más grande.

✔ El bucle Mientras se ejecuta hasta que el usuario decida salir ("S").