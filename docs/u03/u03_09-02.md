# Funciones con Retorno

En la programación el objeto de las funciones es evitar tener que duplicar código, que se repite con frecuencia. Como por ejemplo para mostrar en pantalla varias veces lo mismo.

Pero las funciones van mucho más allá, por ejemplo, permiten realizar unas determinadas operaciones y *devolver* un resultado, para utilizarlo durante el programa.

Por ejemplo, la función **abs()** devuelve el valor absoluto del número, lo que nos permitirá utilizar muchas veces durante el programa y *cargar* su valor en una variable. Siguiendo con el ejemplo:

```
Proceso sumaValoresAbsolutos
	Definir num1, num2, suma, sumaVA como entero;
	Escribir "Escribe dos números:";
	Leer num1;
	Leer num2;
	suma <- num1 + num2;
	sumaVA <- valorAbsoluto(num1) + valorAbsoluto(num2);
	Escribir "La suma es: ", suma;
	Escribir "La suma de sus valores absolutos es: ", sumaVA;
	Escribir "Adios!!!";	
FinProceso

Funcion res <- valorAbsoluto(x)
	Definir res como Entero;
	Si x>0 Entonces
		res <- x;
	SiNo
		res <- -x;
	FinSi
FinFuncion
```

Otro ejemplo:

```
Proceso Mayor
	Definir num1, num2, max Como Entero;
	Definir salir Como Caracter;
	
	Escribir "Escribe el número 1º";
	Leer num1;
	Escribir "Ahora el número 2º";
	Leer num2;
	
	max <- CalcularMax(num1, num2);
	
	Escribir "¿Deseas salir? (S/N)";
	Leer salir;

	Mientras salir <> "S" Hacer
		num1 <- max;
		Escribir "Ahora el siguiente numero";
		Leer num2;
		max <- CalcularMax(num1, num2);
		Escribir "¿Deseas salir? (S/N)";
		Leer salir;
	FinMientras
	
	Escribir "El mayor de todos es ", max;
FinProceso


Funcion res <- CalcularMax(a, b)
	Definir res como Entero;
	Si a > b Entonces
		res <- a;
	SiNo
		res <- b;
	FinSi
FinFuncion
```