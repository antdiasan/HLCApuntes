# Funciones

La programación estructurada permite "subdividir" el programa principal en módulos más pequeños y especializados, de forma que cuando exista una porción de código que se repita muchas veces, se puede codificar como una función independiente, a la que posteriormente podremos invocar, cada vez que la necesitemos.

Pongamos el caso que queremos calcular el máximo de 2 números, el pseucódigo para ello podría ser:

```
Proceso Maximo
	Definir num1, num2, max Como Entero;

	Escribir("Indica el primer número: ");
	Leer num1;
	Escribir("Ahora el segundo: ");
	Leer num2;

	Si (num1>num2) Entonces
		max <- num1;
	SiNo
		max <- num2;
	FinSi

	Escribir "El mayor es: ", max;
FinProceso
```
 
Ahora supongamos que queremos calcular el máximo de 4 números, a los que vamos a llamar num1, num2, num3, num4, el código podría ser algo parecido a lo siguiente:
```
Proceso Maximo
	Definir num1, num2, num3, num4, max1-2, max3-4, max Como Entero;

	Escribir("Indica el primer número: ");
	Leer num1;
	Escribir("Ahora el segundo: ");
	Leer num2;
	Escribir("Indica el tercero número: ");
	Leer num3;
	Escribir("Ahora el cuarto: ");
	Leer num4;

	Si (num1>num2) Entonces
		max1-2 <- num1;
	SiNo
		max1-2 <- num2;
	FinSi

	Si (num3>num4) Entonces
		max3-4 <- num3;
	SiNo
		max3-4 <- num4;
	FinSi

	Si (max1-2>max3-4) Entonces
		max <- max1-2;
	SiNo
		max <- max3-4;
	FinSi

	Escribir "El mayor es: ", max;
FinProceso
```

Donde primero hemos calculado el máximo de los 2 primeros números, y lo hemos guardado en una variable a la que hemos llamado max1-2, y el máximo de los números 3 y 4 lo hemos guardado en una variable a la que hemos llamado max3-4, finalmente hemos comparado estos 2 máximos intermedios (max1-2 y max3-4) y hemos elegido el mayor de ambos.

Ha sido un proceso bastante largo, y eso que sólo lo hemos hecho con 4 variables, puedes imaginar lo que sería con 10 números, o mejor aún con 100.

Existe una forma más limpia, que genera menos código, por lo que también tendremos menos posibilidad de cometer erratas, que es utilizar funciones. 

Para ello se define como módulo independiente del proceso principal, al que se llamará con una serie de argumentos (en nuestro ejemplo serán 2 números cualesquiera) y realizará un proceso con esos datos, finalmente ese proceso puede devolver un resultado o simplemente mostrar algo en pantalla, en nuestro ejemplo devolverá el mayor de los dos números. Veamos el código:

```
Funcion max <- CalcularMaximo(a,b)
	Definir max como Entero;

	Si (a>b) Entonces
		max <- a;
	SiNo
		max <- b;
	FinSi
FinFuncion

Proceso Maximo
	Definir num1, num2, num3, num4, max12, max34, max Como Entero;

	Escribir("Indica el primer número: ");
	Leer num1;
	Escribir("Ahora el segundo: ");
	Leer num2;	
	Escribir("El tercero: ");
	Leer num3;
	Escribir("Y el cuarto: ");
	Leer num4;

	max12 <- CalcularMaximo(num1,num2);
	max34 <- CalcularMaximo(num3, num4);
	max <- CalcularMaximo(max12, max34);

	Escribir "El mayor es: ", max;
FinProceso
```

Cómo puede observarse el código se ha reducido bastante, pero incluso se puede reducir más aún, y reducir el número de variables necesarias:

```
Proceso Maximo
	Definir num1, num2, num3, num4, max Como Entero;

	Escribir("Indica el primer número: ");
	Leer num1;
	Escribir("Ahora el segundo: ");
	Leer num2;	
	Escribir("El terecero: ");
	Leer num3;
	Escribir("Y el cuarto: ");
	Leer num4;	

    //Estamos llamando a la función CalcularMáximo 3 veces 
    //en la misma instrucción
	max <- CalcularMaximo( CalcularMaximo(num1,num2), CalcularMaximo(num3, num4));

	Escribir "El mayor es: ", max;
FinProceso
```
