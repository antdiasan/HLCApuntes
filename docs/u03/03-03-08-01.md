---
title: üìå Ejercicio de clase 31/1/25
---

# Ejercicio de Classroom

üîó [Ejercicio en Google Classroom](https://classroom.google.com/c/NzE4MDgwMDQyNjA3/a/NzQ4MDMxOTI4OTE5/details)

---

## Resoluci√≥n Paso a Paso


## **1Ô∏è‚É£ Comprobaci√≥n de N√∫meros Consecutivos**
**Objetivo:** Comprobar si dos n√∫meros ingresados son consecutivos e indicar el mensaje adecuado.

```pseudocode
Proceso version01
	Definir num1, num2 como Entero;
	
	Escribir "Escribe dos n√∫meros:";
	Leer num1, num2;
    //Hago las dos comprobaciones en un solo Si
	Si num1=(num2+1) o num2=(num1+1) Entonces  
		Escribir num1, " y ", num2, " son consecutivos";
	SiNo
		Escribir num1, " y ", num2, " NO son consecutivos";
	Fin Si
FinProceso
```

---

## **2Ô∏è‚É£ Mostrar el Consecutivo en Caso de No Serlo**
**Objetivo:** Si los n√∫meros no son consecutivos, mostrar el **consecutivo correcto de ambos**.

```pseudocode
Proceso version2
	Definir num1, num2 como Entero;
	
	Escribir "Escribe dos n√∫meros:";
	Leer num1, num2;
    //Hago las dos comprobaciones en un solo Si
	Si num1=(num2+1) o num2=(num1+1) Entonces
		Escribir num1, " y ", num2, " son consecutivos";
	SiNo
		Escribir num1, " y ", num2, " NO son consecutivos";
        //A√±ado los mensajes informando de los verdaderos consecutivos
		Escribir "El consecutivo de ", num1, " es ", num1+1;
		Escribir "El consecutivo de ", num2, " es ", num2+1;
	Fin Si
FinProceso
```

---

## **3Ô∏è‚É£ Implementar un Men√∫ Interactivo**
**Objetivo:** Incluir un men√∫ que verifique que la respuesta ingresada sea v√°lida (`S`, `N`, o mensaje de error).

```pseudocode
Proceso version3
	Definir num1, num2 como Entero;
	Definir operacion como Caracter;

    //Asigno a operacion un valor cualquiera para que entre en el bucle
	operacion <- "Z";

	Mientras operacion <> "N" Hacer
		Escribir "Escribe dos n√∫meros:";
		Leer num1, num2;
		Escribir "Compruebo si son consecutivos?";
		Leer operacion;
		operacion <- Mayusculas(operacion);  //Lo convierto a may√∫sculas

        //Si la operaci√≥n no es "S" o "N" mostrar√° un mensaje de error y lo volver√° a pedir
		Mientras operacion <> "S" y operacion <> "N" Hacer
			Escribir "La operaci√≥n indicada es incorrecta, prueba otra vez";
			Leer operacion;
			operacion <- Mayusculas(operacion);
		FinMientras;
		
		Si num1=(num2+1) o num2=(num1+1) Entonces
			Escribir num1, " y ", num2, " son consecutivos";
		SiNo
			Escribir num1, " y ", num2, " NO son consecutivos";
			Escribir "El consecutivo de ", num1, " es ", num1+1;
			Escribir "El consecutivo de ", num2, " es ", num2+1;
		Fin Si

	FinMientras
FinProceso
```

---

## **4Ô∏è‚É£ Modularizaci√≥n con Funciones**
**Objetivo:** Separar en funciones independientes para optimizar el c√≥digo.

```pseudocode
Proceso version4
	Definir num1, num2 como Entero;
	Definir operacion como Caracter;
	
	operacion <- "Z";

	Mientras operacion <> "N" Hacer
		Escribir "Escribe dos n√∫meros:";
		Leer num1, num2;
		Escribir "Compruebo si son consecutivos?";
		Leer operacion;
		operacion <- Mayusculas(operacion);

		Mientras operacion <> "S" y operacion <> "N" Hacer
			Escribir "Compruebo si son consecutivos?";
			Leer operacion;
			operacion <- Mayusculas(operacion);
		FinMientras;
		
		Si sonConsecutivos(num1,num2) Entonces  //Aqu√≠ estoy llamando a la funci√≥n sonConsecutivos
			Escribir num1, " y ", num2, " son consecutivos";
		SiNo
			Escribir num1, " y ", num2, " NO son consecutivos";
			//En las dos siguientes llamo a la funci√≥n consecutivosDe
			Escribir "El consecutivo de ", num1, " es ", consecutivoDe(num1); 
			Escribir "El consecutivo de ", num2, " es ", consecutivoDe(num2);
		Fin Si
	FinMientras
FinProceso

//Esta funcion devuelve el consecutivo del n√∫mero pasado como argumento
Funcion res <- consecutivoDe(a)
	Definir res como Entero;
	res <- a + 1;
FinFuncion


//Funci√≥n que devuelve si son consecutivos los dos n√∫meros pasados como argumentos
Funcion res <- sonConsecutivos(a, b)
	Definir res como Logico;
	Si a=(b+1) o b=(a+1) Entonces
		res <- Verdadero;
	SiNo
		res <- Falso;
	FinSi
FinFuncion



//Realmente la funci√≥n sonConsecutivos ser√≠a suficiente con:
Funcion res <- sonConsecutivos(a, b)
	res <- a=(b+1) o b=(a+1);
FinFuncion
```

---

